class Championship -- represents championship 

instance variables
	private numberOfGames : nat1;
	private numberOfRounds : nat1;
	private players : seq of Player := [];
	private currentPlayersInChampionship : seq of Player;
	private games : seq of Game;
	
	-- only even number of players
	inv len players rem 2 = 0;
	
operations
	public Championship : seq of Player ==> Championship -- creates championship whith the given number of players
		Championship (allPlayers) == (
			numberOfGames := len allPlayers - 1;
			
			numberOfRounds := (MATH`log(len allPlayers) / MATH`log(2));
	
			players := allPlayers;
			currentPlayersInChampionship := allPlayers;
			games := [];
			
			return self;
		)
	pre len allPlayers > 1 and len players rem 2 = 0;

	pure public GetNumberOfRounds : () ==> nat1 -- return the number of the championship's rounds
		GetNumberOfRounds() == (
			return numberOfRounds;
		);

	pure public GetPlayers : () ==> seq of Player -- return all champioship's players
		GetPlayers() == (
			return players;
		);
		
	pure public GetCurrentPlayersOnChampionship : () ==> seq of Player -- return only the players still in championship
		GetCurrentPlayersOnChampionship() == (
			return currentPlayersInChampionship;
		);
		
	pure public GetGames : () ==> seq of Game -- return all championship's games
		GetGames() == (
			return games;
		);
		
	public AddGames : seq of Game ==> () -- add games to championship
		AddGames(finishedGames) == (
			games := games^finishedGames;
			
			for g in games do (
				g.GetCodeMaker().SumMoves(len g.GetMakerResults());
				g.GetCodeBreaker().SumMoves(len g.GetBreakerMoves());
			);
			
		);
	
	public ShufflePlayers : seq of Player ==> seq of nat1 -- shuffle a sequence of players to be picked by CreateGames
		ShufflePlayers(sequenceOfPlayers) == (
			dcl playerShuffled : int;
			dcl selectedPlayers : seq of nat1 := [];
			
			for p in sequenceOfPlayers do (
				if len selectedPlayers = 0
					then
						playerShuffled := MATH`rand(len sequenceOfPlayers) + 1
				else (
					while playerShuffled in set elems selectedPlayers and len selectedPlayers <> len sequenceOfPlayers do (
						playerShuffled := MATH`rand(len sequenceOfPlayers) + 1;
					);
				);
				
				selectedPlayers := selectedPlayers^[playerShuffled];
			);
			
			return selectedPlayers
		);
		
	public CreateGames : seq of Player ==> seq of Game -- pick pairs of two players from the previous shuffled sequence of players
		CreateGames(sequenceOfPlayers) == (
			dcl playersOrder : seq of nat1 := ShufflePlayers(sequenceOfPlayers);
			dcl generatedGames : seq of Game := [];
			dcl game : Game;
			
			for i = 1 to len sequenceOfPlayers / 2 by 1 do (
				game := new Game(sequenceOfPlayers(playersOrder((i * 2) - 1)), sequenceOfPlayers(playersOrder(i * 2))); 
				generatedGames := generatedGames^[game];
			);
			
			return generatedGames;
		);
		
	public PickAllWinnerPlayers : seq of Game ==> () -- pick players of all games in a round of the championship
		PickAllWinnerPlayers(finishedGames) == (
			dcl winnerPlayers : seq of Player := [];
			
			for i = 1 to len finishedGames by 1 do (
				winnerPlayers := winnerPlayers^[finishedGames(i).GetWinnerPlayer()];
			);
			
			currentPlayersInChampionship := winnerPlayers;
		);
		
	public PrintStats : () ==> () -- returns the number of moves the winner player made during the entire championship
		PrintStats() == (
			IO`print("\nThe winner of championship was ");
			IO`print((hd GetCurrentPlayersOnChampionship()).GetName());
			IO`print(" and has made ");
			IO`print((hd GetCurrentPlayersOnChampionship()).GetNumberOfMoves());
			IO`print(" moves.\n\n");
		);

end Championship